plugins {
    id 'net.neoforged.gradle.userdev' version '7.+'
}

minecraft.accessTransformers.file file('src/main/resources/META-INF/accesstransformer.cfg')

subsystems {
    parchment {
        minecraftVersion = project.mappings_mc_version
        mappingsVersion = project.parchment_version
    }
}

runs {
    configureEach {
        systemProperty 'forge.logging.console.level', 'debug'
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id

        modSource project.sourceSets.main
        modSource project(":Common").sourceSets.main
    }

    client {}

    server {
        programArgument '--nogui'
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = archivesBaseName
            artifact jar
            artifact sourcesJar
        }
    }
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN")

    project {
        id = curseforge_id
        changelog = 'https://github.com/Tslat/TslatEffectsLib/commits/1.20.4'
        releaseType = 'release'
        addGameVersion minecraft_version

        mainArtifact(jar, artifact -> {
            artifact.displayName = "${project.mod_name} NeoForge ${project.minecraft_version}-${project.version}"
        })
    }
}

modrinth {
    token = System.getenv("modrinthKey")
    projectId = mod_id
    versionNumber = project.version
    versionType = "release"
    uploadFile = jar
    gameVersions = ["${project.minecraft_version}"]
    loaders = ["neoforge"]
}